# {{ ansible_managed }}
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.zsh_config

# your project folder that we can `c [tab]` to
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -e ~/.localrc ]]
then
  source ~/.localrc
fi

# Create $HOME/.zsh if missing
if [[ ! -e ~/.zsh ]]
then
  echo "creating .zsh"
  mkdir ~/.zsh
fi

# Zplug initialization
if [[ -e /usr/share/zplug/init.zsh ]]
then
    source /usr/share/zplug/init.zsh
    zplug "mafredri/zsh-async", from:"github", use:"async.zsh"
    zplug "zsh-users/zsh-autosuggestions", from:"github", use:"zsh-autosuggestions.zsh"

    if ! zplug check
    then
        zplug install
    fi
    zplug load

    # Z-autosuggestions
    ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=50
fi

typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Activate zsh-completions
#fpath=(/usr/local/share/zsh-completions $fpath)
fpath=(${HOME}/.zsh $fpath)

# Set prompt, thanks vinz!
_vbe_setprompt
